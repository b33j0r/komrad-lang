WsHandler = {
    server = null
    socket = null

    [ws _socket connected] {
        socket = socket
    }

    [ws _socket disconnected] {
        server disconnect me
    }

    [ws _socket message _msg] {
        Log info msg
        server broadcast me msg
    }

    [send _msg] {
        Log info msg
        socket send msg
    }
}

Server = {
    clients = []
    history = []
    listener = null
    host = "localhost"
    port = 8080

    [start] {
        listener = spawn HttpListener {
            host: host,
            port: port,
            delegate: me
        }
        Log info "Server started on http://" + host + ":" + port
    }

    [http _request _response GET] {
        response template "examples/web/templates/chat.html" {
          title: "Komrad"
        }
    }

    [ws _socket connect] {
        client = spawn WsHandler {
            socket: socket,
            server: me
        }
        clients = clients + [client]
        socket set-delegate client
        Log info "WebSocket connected"
        history foreach m {
            socket send m
        }
    }

    [disconnect _client] {
        clients = clients retain x {
            x != client
        }
        Log info "WebSocket disconnected"
    }

    [broadcast _sender _message] {
        clients foreach x {
            x send message
        }
        history = history + [message]
    }
}

server = spawn Server {
    host: "0.0.0.0",
    port: 8086
}
